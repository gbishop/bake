Recipe: statements *= Statement[/\n+/] /\s*/;

Statement: ( Units | Part | Text ) ;

Text: /^.*$/ ;

Units: 'Units' '=' units=/[g%]/;

Part: part=PartName ':' '\n' ingredients*=Ingredient['\n'];

PartName: name=ID;

Ingredient: ('hydration' '=' hydration=BP ) |
            ('scale' '=' scale=BP) | 
            (name=ID (('[' parameter=Parameter ']') |
                      ('=' expr=Sum) |
                      ('limit' limit=BP)
                     )? );

Parameter: name=ID '=' value=Value;

Sum: term=Product sums*=Sums;

Sums: op=/[+-]/ term=Product;

Product: factor=Value products*=Products;

Products: '*' factor=Value;

Value: value=BP | named=NamedValue | '(' expr=Sum ')';

BP: value=NUMBER unit=/[g%]?/;

NamedValue: part=ID ('.' ingredient=ID)?;

Comment: /\/\/.*?$|(?ms:\/\*.*?\*\/\n+)/;
